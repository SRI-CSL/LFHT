lfht_4: CONTEXT =
BEGIN
  % N : int = 10;
  % T : int = 6;
  % THRESHOLD : real = 6/10;
  N : int = 6;
  T : int = 8;
  THRESHOLD : real = 6/10;
  NUM_THREADS: nat = 4;	
  table_index: TYPE = [1 .. N];
  descriptor: TYPE = [# num_entries : nat, num_to_migrate: nat, assimilated : bool #];
  table: TYPE = ARRAY table_index OF descriptor;
  empty_descriptor : descriptor = (# num_entries := 0, num_to_migrate := 0, assimilated := FALSE #);
  empty_table : table = [ [i: table_index] empty_descriptor ];

min (a : nat, b: nat): nat = IF  a < b THEN a ELSE b ENDIF;


table_size (i: table_index, k:nat): bool =
  (i=1 and k=32) or (i=2 and k=64) or (i=3 and k=128) or (i=4 and k=256) or
  (i=5 AND k=512) or (i=6 AND k=1024);

full_table (i: table_index, k:nat): bool =
  (i=1 and k >= 32 * THRESHOLD) or  (i=2 and k >= 64 * THRESHOLD) or
  (i=3 and k >= 128 * THRESHOLD) or (i=4 and k >= 256 * THRESHOLD) or
  (i=5 and k >= 512 * THRESHOLD) or (i=6 and k >= 1024 * THRESHOLD) ;

max_size(i: table_index): nat =
  IF i=1 THEN 32
  ELSIF i=2 THEN 64
  ELSIF i=3 THEN 128
  ELSIF i=4 THEN 256
  ELSIF i=5 THEN 512
  ELSE 1024
  ENDIF;

Migrated(ht: table, i: table_index): nat =
   (ht[i].num_entries - ht[i].num_to_migrate) ;

thread: TYPE = [1 .. NUM_THREADS];

add : MODULE = 
 BEGIN
 GLOBAL
  K : nat,
  old, new : table_index,
  pending : int,
  did_not_pay : int,
  paid_tax : int,
  revenue : int,
  posted : int,
  ht : table
 OUTPUT
  pc : [0 .. 3]
 INITIALIZATION
   pc = 0;
   pending = 0;
   did_not_pay = 0;
   paid_tax = 0;
   posted = 0;
   revenue = 0;
   K = 32;
   ht = empty_table;
   old = 1;
   new = 1
TRANSITION
[ pc = 0 and old /= new and not ht[old].assimilated --> 
        % moveN    
        ht' = (ht WITH [new].num_entries := ht[new].num_entries + min (T,ht[old].num_to_migrate)) WITH [old].num_to_migrate := ht[old].num_to_migrate - min (T,ht[old].num_to_migrate) WITH [old].assimilated := (ht[old].num_to_migrate <= T);


        % ht'[new].num_entries = ht[new].num_entries + min (T,ht[old].num_to_migrate);
        % ht'[old].num_to_migrate = ht[old].num_to_migrate - min (T,ht[old].num_to_migrate);
        % ht'[old].assimilated = (ht'[old].num_to_migrate = 0);
	pending' = pending + 1;
	paid_tax' = paid_tax + 1;
	revenue' = revenue + min (T,ht[old].num_to_migrate);
        pc' = 1
  []
  pc = 0 and not( old /= new and not ht[old].assimilated) -->  pc' = 1; pending' = pending + 1;
  []
  pc = 1 and ht[new].num_entries < K -->
        ht'[new].num_entries = ht[new].num_entries + 1;
	pending' = pending - 1 ;
        posted' = posted + 1;
        pc' = 2  
  []
  pc = 1 and ht[new].num_entries >= K -->
	pending' = pending - 1 ;
	did_not_pay' = did_not_pay - 1;
	pc' = 0
  []
  pc = 2 and ht[new].num_entries > THRESHOLD*K and new < N --> 
       % grow
       K' = 2 * K;
       old' = new;
       new' = new + 1;
       ht' = (ht WITH [new+1].num_entries := 0) WITH [new+1].num_to_migrate := 0 WITH [new+1].assimilated := FALSE WITH [new].num_to_migrate := ht[new].num_entries;
       
       % ht'[new+1].num_entries = 0;       
       % ht'[new+1].num_to_migrate = 0;       
       % ht'[new+1].assimilated = FALSE;       
       % ht'[new].num_to_migrate = ht[new].num_entries;       
       did_not_pay' = pending;
       paid_tax' = 0;
       revenue' = 0;
       posted' = 0;
       pc' = 0
  []
  pc = 2 and ht[new].num_entries <= THRESHOLD*K --> pc' = 0
  []
  pc = 2 and ht[new].num_entries > THRESHOLD*K  and new >= N --> pc' = 3
  []
  pc = 3 --> pc' = 3 
]

END;

system: MODULE = WITH OUTPUT pc: ARRAY thread OF [0 .. 3]
      ([] (i: thread): (RENAME pc TO pc[i] IN add));

% PROVED
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal lemma1
lemma1: LEMMA system |- G((new = 1 and old = 1) or (new = old+1));

% PROVED
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal lemma2 -l lemma1
lemma2: LEMMA system |- G(FORALL (i: table_index): i > new => ht[i].num_entries = 0 and ht[i].num_to_migrate=0 and not ht[i].assimilated);

% PROVED
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal lemma2a
lemma2a: LEMMA system |- G(FORALL (i: table_index): NOT full_table(i, ht[i].num_entries) AND table_size(i, ht[i].num_entries) => FORALL (j: table_index): j > i => ht[j].num_entries = 0 and ht[j].num_to_migrate=0 and not ht[j].assimilated);

% PROVED
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal lemma3
lemma3: LEMMA system |- G(FORALL (i: table_index): ht[i].num_to_migrate <= ht[i].num_entries);

% PROVED
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal lemma4
lemma4: LEMMA system |- G(table_size (new, K));

% PROVED
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal lemma5a -l lemma1 -l lemma4
lemma5a: LEMMA system |- G(NOT ht[new].assimilated);

% PROVED
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal lemma5b -l lemma1 -l lemma4
lemma5b: LEMMA system |- G(ht[new].num_to_migrate = 0);

% PROVED
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal lemma5c -l lemma1 -l lemma4
lemma5c: LEMMA system |- G(old = new OR full_table(old, ht[old].num_entries));

% PROVED
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal lemma5e -l lemma4 -l lemma5c
lemma5e: LEMMA system |- G(FORALL (i: table_index): NOT full_table(i, ht[i].num_entries) => NOT ht[i].assimilated);

% PROVED
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal lemma5f -l lemma4
lemma5f: LEMMA system |- G(FORALL (i: table_index): NOT full_table(i, ht[i].num_entries) => ht[i].num_to_migrate=0);

% PROVED
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal lemma5g -l lemma1 -l lemma2 -l lemma3 -l lemma4  -l lemma5a -l lemma5b -l lemma5c -l lemma5e -l lemma5f 
lemma5g: LEMMA system |- G(FORALL (i: table_index): ht[i].assimilated => ht[i].num_to_migrate=0);

% PROVED
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal final
final : LEMMA
    system |- G(pc[1] = 3 OR pc[2] = 3 OR pc[3] = 3 OR pc[4] = 3 => new = N);

% PROVED
% sal-inf-bmc -i -ice -v 1 -d 15 -s yices2 lfht_4.sal lemma6b -l lemma1 -l lemma2 -l lemma3 -l lemma4 -l lemma5a -l lemma5b -l lemma5c -l lemma5e -l lemma5f -l lemma5g -l final
lemma6b : LEMMA
    system |- G(FORALL (i: table_index):
	           i < old  => full_table(i, ht[i].num_entries) AND
		               (ht[i].num_to_migrate = 0 <=> ht[i].assimilated));


% PROVED
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal lemma6 -l lemma1 -l lemma2 -l lemma3 -l lemma4 -l lemma5a -l lemma5b -l lemma5c -l lemma5e -l lemma5f -l lemma5g 
lemma6 : LEMMA
    system |- G(FORALL (i: table_index):
	           i <= old AND old < new =>
		        (full_table(i, ht[i].num_entries) => (ht[i].num_to_migrate = 0 <=> ht[i].assimilated)) AND
                        (NOT full_table(i, ht[i].num_entries) => NOT (ht[i].assimilated AND ht[i].num_to_migrate = 0)));


% PROVED
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal lemma8b -l lemma1 -l lemma2 -l lemma3 -l lemma4 -l lemma5a -l lemma5b -l lemma5c -l lemma5e -l lemma5f -l lemma5g -l lemma6 
lemma8b : LEMMA
    system |- G((old /= new AND Migrated(ht,old) = 0 AND ht[new].num_entries = 0) => 
 	        (pc[1] = 0 OR pc[2] = 0 OR pc[3] = 0 OR pc[4] = 0));

% PROVED
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal pending
pending: LEMMA system |- G( pending = (IF pc[1] = 1 THEN 1 ELSE 0 ENDIF) + (IF pc[2] = 1 THEN 1 ELSE 0 ENDIF) + (IF pc[3] = 1 THEN 1 ELSE 0 ENDIF) + (IF pc[4] = 1 THEN 1 ELSE 0 ENDIF));

% PROVED
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal pending_pos -l pending
pending_pos : LEMMA system |- G( pending >= 0 );

% PROVED
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal pending_upper -l pending
pending_upper : LEMMA system |- G( pending <= NUM_THREADS   );


% PROVED
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal did_not_pay_1 -l pending_pos -l pending
did_not_pay_1 : LEMMA
      system |- G( did_not_pay < NUM_THREADS);		

% PROVED
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal paid_tax_0
paid_tax_0 : LEMMA
      system |- G( old = new => paid_tax = 0 );		
% PROVED
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal paid_tax_1 -l pending_pos -l pending
paid_tax_1 : LEMMA
      system |- G( paid_tax >= 0);		

% PROVED
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal revenue_0
revenue_0 : LEMMA
      system |- G( old = new => revenue = 0 );		

% PROVED
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal revenue_1 -l pending_pos -l pending
revenue_1 : LEMMA
      system |- G( revenue >= 0);		

% PROVED
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal revenue_2 
revenue_2 : LEMMA
      system |- G( old < new =>  revenue = Migrated(ht,old));		

% PROVED
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal revenue_3 -l revenue_0 -l revenue_1 -l revenue_2
revenue_3 : LEMMA
      system |- G( old < new => revenue <= ht[old].num_entries);		

% PROVED
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal revenue_4 -l revenue_0 -l revenue_1 -l revenue_2 -l revenue_3
revenue_4 : LEMMA
      system |- G( old < new AND ht[old].num_to_migrate > 0 => revenue = T * paid_tax);

% PROVED
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal revenue_5 -l revenue_0 -l revenue_1 -l revenue_2 -l revenue_3 -l revenue_4
revenue_5 : LEMMA
      system |- G( old < new AND ht[old].num_to_migrate = 0 => revenue = ht[old].num_entries);

% PROVED
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal posted_0 -l pending_pos -l pending
posted_0 : LEMMA
      system |- G( posted >= 0);		

% PROVED
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal posted_1 -l posted_0 -l paid_tax_0 -l paid_tax_1 -l pending_pos -l pending -l lemma1 -l lemma2 -l lemma2a -l lemma3 -l lemma4  -l lemma5a -l lemma5b -l lemma5c -l lemma5e -l lemma5f -l lemma5g
posted_1 : LEMMA
      system |- G( old /= new AND not ht[old].assimilated => 
	           posted + pending =  paid_tax + did_not_pay);		
		       

% PROVED
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal upper_strengthening_2 -l pending_pos -l pending -l lemma1 -l lemma2 -l lemma2a -l lemma3 -l lemma4 -l lemma5a -l lemma5b -l lemma5c -l lemma5e -l lemma5f -l lemma5g
upper_strengthening_2 :  LEMMA
       system |- G( (old /= new => ht[new].num_entries <= T * paid_tax + posted));


% PROVED
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal upper_strengthening_3 -l pending_pos -l pending  -l lemma1 -l lemma2 -l lemma2a -l lemma3 -l lemma4 -l lemma5a -l lemma5b -l lemma5c -l lemma5e -l lemma5f -l lemma5g
upper_strengthening_3 :  LEMMA
       system |- G( (old /= new AND ht[old].num_to_migrate >= T => ht[new].num_entries = T * paid_tax + posted));

% PROVED
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal upper_strengthening_3a -l pending_pos -l pending  -l lemma1 -l lemma2 -l lemma2a -l lemma3 -l lemma4 -l lemma5a -l lemma5b -l lemma5c -l lemma5e -l lemma5f -l lemma5g
upper_strengthening_3a :  LEMMA
       system |- G( (old /= new => ht[new].num_entries <= T * paid_tax + posted));

% PROVED 
% sal-inf-bmc -i -ice -v 1 -d 10 -s yices2 lfht_4.sal upper_strengthening_5 -l pending_pos -l pending -l lemma1 -l lemma2 -l lemma2a -l lemma3 -l lemma4 -l lemma5a -l lemma5b -l lemma5c -l lemma5e -l lemma5f -l lemma5g 
upper_strengthening_5 :  LEMMA
       system |- G( (old /= new => ht[new].num_entries = Migrated(ht, old) + posted));

% PROVED 
% sal-inf-bmc -i -ice -v 1 -d 13 -s yices2 lfht_4.sal upper_global -l pending_pos -l pending -l lemma1 -l lemma2 -l lemma2a -l lemma3 -l lemma4 -l lemma5a -l lemma5b -l lemma5c -l lemma5e -l lemma5f -l lemma5g -l lemma6 -l lemma8b -l upper_strengthening_2 -l upper_strengthening_3 -l upper_strengthening_3a -l paid_tax_0 -l paid_tax_1 -l posted_0 -l posted_1 -l did_not_pay_1  -l lemma6b  -l upper_strengthening_5 -l revenue_0 -l revenue_1 -l revenue_2 -l final -l revenue_3 -l revenue_4 -l revenue_5
upper_global :  LEMMA
      system |- G( (FORALL (i: table_index): i < N => ht[i].num_entries <= (THRESHOLD * max_size(i)) + NUM_THREADS));
		   

% PROVED  
% sal-inf-bmc -i -ice -v 1 -d 13 -s yices2 lfht_4.sal upper -l pending_pos -l pending  -l lemma1 -l lemma2 -l lemma2a -l lemma3 -l lemma4 -l lemma5a -l lemma5b -l lemma5c -l lemma5e -l lemma5f -l lemma5g -l lemma6 -l lemma8b -l upper_strengthening_2 -l upper_strengthening_3 -l upper_strengthening_3a -l paid_tax_0 -l paid_tax_1 -l posted_0 -l posted_1 -l did_not_pay_1 -l lemma6b  -l upper_strengthening_5 -l revenue_0 -l revenue_1 -l revenue_2 -l final -l revenue_3 -l revenue_4 -l revenue_5 -l upper_global
upper :  LEMMA
      system |- G( (old /= new  AND not ht[old].assimilated  =>  
                    (ht[new].num_entries <= Migrated(ht,old) + (Migrated(ht,old) / T) +  (NUM_THREADS-1) + pending)));


% PROVED 
% sal-inf-bmc -i -ice -v 1 -d 20 -s yices2 lfht_4.sal sane_past -l pending_pos -l pending -l lemma1 -l lemma2 -l lemma2a -l lemma3 -l lemma4 -l lemma5a -l lemma5b -l lemma5c -l lemma5e -l lemma5f -l lemma5g -l upper_global -l upper
sane_past :  LEMMA
      system |- G(
		   (FORALL (i: table_index):
		        i < old  => full_table(i, ht[i].num_entries) AND
			            ht[i].num_to_migrate = 0 AND ht[i].assimilated));

% PROVED
% sal-inf-bmc -i -ice -v 1 -d 15 -s yices2 lfht_4.sal prop1 -l upper -l upper_global  -l pending_pos -l pending  -l lemma1 -l lemma2 -l lemma2a -l lemma3 -l lemma4 -l lemma5a -l lemma5b -l lemma5c -l lemma5e -l lemma5f -l lemma5g 
prop1 : LEMMA system |- G(FORALL (i: table_index): i < old => ht[i].assimilated);

END
